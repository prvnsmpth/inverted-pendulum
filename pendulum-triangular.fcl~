// Block definition (there may be more than one block per file)
FUNCTION_BLOCK tipper

// Define input variables
VAR_INPUT
    theta : REAL;
    omega : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    current : REAL;
END_VAR

// Fuzzify input variable 'theta'
FUZZIFY theta
    TERM zero := (-0.5, 0) (0, 1) (0.5, 0) ; 
    TERM small_pos := (0, 0) (0.65, 1) (0.75, 1) (1.2, 0);
    TERM small_neg := (-1.2, 0) (-0.75, 1) (-0.65, 1) (-0.2, 0);
END_FUZZIFY

// Fuzzify input variable 'omega'
FUZZIFY omega
    TERM zero := (-10, 0) (0.0, 1) (10, 0) ; 
    TERM small_pos := (0, 0) (15, 1) (17, 1) (30, 0);
    TERM small_neg := (-30, 0) (-17, 1) (-15, 1) (0, 0);
END_FUZZIFY

// Defzzzify output variable 'current'
DEFUZZIFY current
    TERM zero := (-6, 0) (0.0, 1) (6, 0) ; 
    TERM small_pos := (0, 0) (9, 1) (9, 1) (18, 0);
    TERM small_neg := (-18, 0) (-9, 1) (-9, 1) (0, 0);
    TERM med_pos := (11, 0) (20, 1) (20, 1) (29, 0);
    TERM med_neg := (-29, 0) (-20, 1) (-20, 1) (-11, 0);
    
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;

    RULE 1 : IF theta IS small_neg AND omega IS small_neg 
                THEN current IS med_pos;
                
    RULE 2 : IF theta IS zero AND omega IS small_neg 
                THEN current IS small_pos;
                
    RULE 3 : IF theta IS small_pos AND omega IS small_neg 
                THEN current IS zero;
                
    RULE 4 : IF theta IS small_neg AND omega IS zero 
                THEN current IS small_pos;
        
    RULE 5 : IF theta IS zero AND omega IS zero 
                THEN current IS zero;
                
    RULE 6 : IF theta IS small_pos AND omega IS zero
                THEN current IS small_neg;
                
    RULE 7 : IF theta IS small_neg AND omega IS small_pos 
                THEN current IS zero;
                
    RULE 8 : IF theta IS zero AND omega IS small_pos 
                THEN current IS small_neg;
                
    RULE 9 : IF theta IS small_pos AND omega IS small_pos 
                THEN current IS med_neg;

END_RULEBLOCK

END_FUNCTION_BLOCK
